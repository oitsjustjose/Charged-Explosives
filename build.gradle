plugins {
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0+"
    id "net.neoforged.gradle" version "[6.0.18,6.2)"
}

group = 'com.oitsjustjose.charged_explosives'
version = "${mod_version}"
base { archivesName = "ChargedExplosives-${game_version}-${mod_version}" }

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
minecraft {
    mappings channel: 'official', version: "${game_version}"
    copyIdeResources = true
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { charged_explosives { source sourceSets.main } }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods { charged_explosives { source sourceSets.main } }
        }
    }
}


dependencies {
    minecraft "net.neoforged:forge:${game_version}-${neo_version}"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "charged_explosives",
                "Specification-Vendor"    : "oitsjustjose.com",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${mod_version}",
                "Implementation-Vendor"   : "oitsjustjose.com",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.register('publish') {
    dependsOn 'curseforge'
    dependsOn 'modrinth'
}

curseforge {
    apiKey = project.hasProperty("CURSEFORGE_API_KEY") ? project.CURSEFORGE_API_KEY : ""
    project {
        id = '693885'
        changelogType = 'markdown'
        changelog = file("./changelog.md")
        releaseType = 'release'
        addGameVersion "${game_version}"
        mainArtifact(jar) {
            displayName = "Charged Explosives ${game_version} ${mod_version}"
        }
    }
}

modrinth {
    token = project.hasProperty("MODRINTH_API_KEY") ? project.MODRINTH_API_KEY : ""
    projectId = "sv2l2OG2"
    versionName = "Charged Explosives ${game_version} ${mod_version}"
    versionNumber = "${game_version}-${mod_version}"
    versionType = "release"
    uploadFile = jar
    gameVersions = ["${game_version}"]
    loaders = ["forge"]
    changelog = file("./changelog.md").text
}
